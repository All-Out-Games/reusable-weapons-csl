Spread_Shot_Ability :: class : Ability_Base {
    on_init :: proc(ability: Ability_Base) {
        ability.name = "Blast";
        ability.icon = get_asset(Texture_Asset, "reusable_weapons/sprites/reusable-weapons/weapon_icons/weapon_icons_250x250/shotgun.png");
        ability.disable_keybind = true;
        ability.is_aimed_ability = true;
    }

    can_use :: proc(ability: Ability_Base) -> bool {
        p := ability.player.(Player);
        if p.equipped_weapon_type == .NONE return false;
        config := get_weapon_config(p.equipped_weapon_type);
        if config.fire_mode != .SPREAD return false;
        return weapon_has_ammo(p, p.equipped_weapon_type);
    }

    on_update :: proc(ability: Ability_Base, params: ref Ability_Update_Params) {
        p := ability.player.(Player);
        wt := p.equipped_weapon_type;
        config := get_weapon_config(wt);

        ability.icon = get_asset(Texture_Asset, config.weapon_icon_path);

        aiming := Ability_Utilities.update_aiming_ability(p, ref params);

        if aiming.aim {
            p.last_aim_direction = aiming.aim_direction;
            p->set_mouse_ik_enabled(true);
            p->set_aim_target(p.entity.world_position + aiming.aim_direction * 5.0);
            draw_thin_aiming_line(p.entity.world_position, aiming.aim_direction, 1.0);
        } else {
            p->set_mouse_ik_enabled(false);
        }

        should_fire := false;
        if p.device_kind == .PC {
            should_fire = aiming.activate;
        } else {
            should_fire = aiming.aim;
        }

        if should_fire && params.can_use && ability.current_cooldown <= 0 {
            weapon_shoot(p, wt);

            if Game.is_server() {
                // Spawn multiple projectiles in a spread pattern
                count := config.projectile_count;
                total_spread := config.spread_angle;
                dir := aiming.aim_direction;
                base_angle := atan2(dir.y, dir.x);

                i := 0;
                while i < count {
                    offset_angle: float = 0;
                    if count > 1 {
                        t := i.(float) / (count - 1).(float);
                        offset_angle = (t - 0.5) * total_spread * 3.14159 / 180.0;
                    }
                    final_angle := base_angle + offset_angle;
                    spread_dir := v2{cos(final_angle), sin(final_angle)};

                    if wt == .SUNNY_SIDE_SHOTTY {
                        spawn_aoe_projectile(p, spread_dir, wt);
                    } else {
                        spawn_aoe_projectile(p, spread_dir, wt);
                    }
                    i += 1;
                }
            }

            ability.current_cooldown = config.fire_rate;
        }

        if should_fire && !weapon_has_ammo(p, wt) {
            if p->is_local() {
                Notifier.notify("Out of ammo!");
            }
        }
    }
}
